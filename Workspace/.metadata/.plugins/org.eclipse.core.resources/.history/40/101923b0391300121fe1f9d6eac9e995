RpcRoute = require('../../../RpcRoute')
QRefDatabase = require('../../../../db/QRefDatabase')
RpcResponse = require('../../../../serialization/RpcResponse')
UserAuth = require('../../../../security/UserAuth')
https = require('https')
async = require('async')
fs = require('fs')
###
Service route that saves an uploaded productIcon and returns the final path to image.
@example Service Methods (see {ProductIconRpcRequest})
  Request Format: application/json
  Response Format: application/json
  
  POST /services/rpc/aircraft/product/icon
    @BODY - (Required) ProductIconRpcRequest
    
	Returns the next available version number in the returnValue field of the response object.
@author Nathan Klick
@copyright QRef 2012
###
class ProductIconRoute extends RpcRoute
	constructor: () ->
		super [{ method: 'POST', path: '/product/icon' }, { method: 'GET', path: '/product/icon' }]
	post: (req, res) =>
		if not @.isValidRequest(req)
			resp = new RpcResponse(null)
			resp.failure('Bad Request', 400)
			res.json(resp, 200)
			return
		
		db = QRefDatabase.instance()
		fileKeys = Object.keys(req.files)
		token = req.param('token')
		productId = req.body.product
		
		fsPath = '/storage/http/qref/WebContent/images/products/icons/'
		webPath = 'images/products/icons/'
		
		if fileKeys.length == 0 or fileKeys.length > 1
			resp = new RpcResponse(null)
			resp.failure('Bad Request', 400)
			res.json(resp, 200)
			return
		
		db.Product.findById(productId, (err, product) ->
			if err?
				resp = new RpcResponse(null)
				resp.failure('Internal Error', 500)
				res.json(resp, 200)
				return
			
			if not product?
				resp = new RpcResponse(null)
				resp.failure('Not Found', 404)
				res.json(resp, 200)
				return
			
			file = req.files[fileKeys[0]]
			slashPos = file.path.lastIndexOf('/')
			
			if slashPos > 0
				fileName = file.path.slice(slashPos+1)
			else
				fileName = file.path
			
			targetPath = fsPath + fileName
			targetWebPath = webPath + fileName
			
			fs.rename(file.path, targetPath, (err) ->
				if err?
					resp = new RpcResponse(null)
					resp.failure('Internal Error', 500)
					res.json(resp, 200)
					return
				
				product.productIcon = targetWebPath
				
				product.save((err) ->
					if err?
						resp = new RpcResponse(null)
						resp.failure('Internal Error', 500)
						res.json(resp, 200)
						return
					
					resp = new RpcResponse(targetWebPath)
					res.json(resp, 200)
					return
				)
			)
			
			
			
			
		)
		
	
	isValidRequest: (req) ->
		if req.body? and req.body?.mode? and req.body.mode == 'rpc' and req.body?.product? and req.body?.token?
			true
		else
			false
module.exports = new ProductIconRoute()